// Note: nothing is case insensitive here because it is assumed to only parse lowercase input.

Field
  = tincture:Tincture _ main:Main? _ "."? { return ["field", { tincture }, main] }
  / "party" _ "per" _ direction:Direction _ dexter:Tincture _ "and" _ sinister:Tincture _ main:Main? "."? { return ["partyPerField", { direction, dexter, sinister }, main] }

Main
  = ShapeGroup
  / "on" _ bg:SingularShape _ fg:ShapeGroup { return ["on", { bg, fg }] }
  / "on" _ bg:SingularShape _ "between" _ surround:PluralShape _ fg:ShapeGroup { return ["on", { bg, fg, surround }] }

ShapeGroup
  = shape:SingularShape
  / shape:PluralShape

SingularShape
  = Singular _ type:Shape _ tincture:Tincture { return [type, { tincture, count: 1 }] }

Singular
  = "a"
  / "an"

PluralShape
  // TODO: Repetition is only permitted when this is not part of an 'on'; how to express?
  = count:Plural _ type:Shape "s" _ repetition:Repetition? _ tincture:Tincture { return [type, { tincture, repetition, count }] }

Plural
  = "two" { return 2 }
  / "three" { return 3 }
  / "four" { return 4 }
  / "five" { return 5 }
  / "six" { return 6 }
  / "seven" { return 7 }
  / "eight" { return 8 }
  / "nine" { return 9 }
  / "ten" { return 10 }
  / "eleven" { return 11 }
  / "twelve" { return 12 }

Direction
  = "pale"
  / "fess"

Repetition
  = "palewise"
  / "fesswise"

Tincture
  = "azure"
  / "or"
  / "argent"
  / "gules"
  / "vert"
  / "sable"

Shape
  = "bend"
  / "sword"
  / "fess"
  / "rondel"
  / "mullet"
  / "ignore this because peggy breaks weirdly on the last rule here"

_ "whitespace"
  // Note that comma is considered, effectively, whitespace. It is not used to delimit anything and is never required.
  = [, \t\n\r]*
