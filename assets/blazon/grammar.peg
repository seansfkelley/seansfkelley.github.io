// Note: nothing is case insensitive here because it is assumed to only parse lowercase input.

Field
  = tincture:Tincture _ main:Main? _ "."? { return ["field", { tincture }, main] }
  / "party"? _ "per" _ direction:Direction _ first:Tincture _ "and" _ second:Tincture _ main:Main? "."? { return ["partyPerField", { direction, first, second }, main] }

Main
  = Charge
  / "on" _ bg:SingularCharge _ fg:Charge { return ["on", { bg, fg }] }
  / "on" _ bg:SingularCharge _ "between" _ surround:PluralCharge _ fg:Charge { return ["on", { bg, fg, surround }] }

Charge
  = SingularCharge
  / PluralCharge

SingularCharge
  = Singular _ charge:ChargeName _ tincture:Tincture { return [charge, { tincture, count: 1 }] }

Singular
  = "a"
  / "an"

PluralCharge
  // TODO: Repetition is only permitted when this is not part of an 'on'; how to express?
  = count:Plural _ charge:ChargeName "s" _ repetition:Repetition? _ tincture:Tincture { return [charge, { tincture, repetition, count }] }

Plural
  = "two" { return 2 }
  / "three" { return 3 }
  / "four" { return 4 }
  / "five" { return 5 }
  / "six" { return 6 }
  / "seven" { return 7 }
  / "eight" { return 8 }
  / "nine" { return 9 }
  / "ten" { return 10 }
  / "eleven" { return 11 }
  / "twelve" { return 12 }

// TODO: bend, etc.
Direction
  = "pale"
  / "fess"

Repetition
  = "palewise"
  / "fesswise"

Tincture
  = "azure"
  / "or"
  / "argent"
  / "gules"
  / "vert"
  / "sable"
  / "purpure"

ChargeName
  = "bend"
  / "sword"
  / "fess"
  / "rondel"
  / "mullet"
  / "cross"
  / "chevron"
  / "pale"
  / "saltire"
  / "chief"

_ "whitespace"
  // Note that comma is considered, effectively, whitespace. It is not used to delimit anything and is never required.
  = [, \t\n\r]*
