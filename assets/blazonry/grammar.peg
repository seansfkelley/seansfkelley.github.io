// Note: nothing is case insensitive here because it is assumed to only parse lowercase input.

Field
  = tincture:Tincture _ elements:Main? _ "."? { return { tincture, elements } }
  / "party"? _ "per" _ direction:Direction _ first:Tincture _ "and" _ second:Tincture _ elements:Main? "."? { return { direction, first, second, elements } }

Main
  = Ordinary
  / Charge
  / "on" _ ordinary:Ordinary _ charge:Charge { return { on: true, ordinary, charge } }
  / "on" _ ordinary:Ordinary _ "between" _ surround:PluralCharge _ charge:Charge { return { on: true, ordinary, surround, charge } }

Ordinary
  = Singular _ ordinary:OrdinaryName _ tincture:Tincture { return { ordinary, tincture } }

Charge
  = SingularCharge
  / PluralCharge

SingularCharge
  = Singular _ charge:ChargeName _ posture:Posture? _ tincture:Tincture { return { charge, tincture, posture, count: 1 } }

PluralCharge
  = count:Plural _ charge:ChargeName "s" _ posture:Posture? _ tincture:Tincture _ direction:Direction? { return { charge, tincture, posture, count, direction } }

Singular "a/an"
  = "a"
  / "an"

Plural "plural quantity"
  = "two" { return 2 }
  / "three" { return 3 }
  / "four" { return 4 }
  // / "five" { return 5 }
  // / "six" { return 6 }

// TODO: bend, etc.
Direction
  = "in pale" { return "pale" }
  / "in fess" { return "fess" }

Posture
  = "palewise"
  / "fesswise"

Tincture "tincture"
  = "azure"
  / "or"
  / "argent"
  / "gules"
  / "vert"
  / "sable"
  / "purpure"
  / "counterchanged"

OrdinaryName "ordinary name"
  = "bend"
  / "fess"
  / "cross"
  / "chevron"
  / "pale"
  / "saltire"
  / "chief"

ChargeName "charge name"
  = "sword"
  / "rondel"
  / "mullet"

_ "whitespace"
  // Note that comma is considered, effectively, whitespace. It is not used to delimit anything and is never required.
  = [, \t\n\r]*
